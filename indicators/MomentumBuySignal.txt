// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © giantguppy
// This indicator is only meant for a 5M Heiken-Ashi chart.

//@version=6
indicator("MomentumBuySignal", overlay=true)

CLEAR_COLOR             = color.rgb(0, 0, 0, 0)
BEARISH_COLOR           = color.rgb(180, 0, 0)
BULLISH_COLOR           = color.rgb(0, 160, 0)
BEARISH_COLOR_DIM       = color.rgb(150, 0, 0)
BULLISH_COLOR_DIM       = color.rgb(0, 130, 0)
BEARISH_COLOR_BRIGHT    = color.rgb(240, 60, 60)
BULLISH_COLOR_BRIGHT    = color.rgb(60, 220, 60)
SIDEWAYS_COLOR          = color.rgb(80, 80, 80)
SQUEEZE_COLOR           = color.rgb(0, 65, 255, 20)
BUY_LABEL_MARGIN        = 8
DIR_BEARISH             = -1
DIR_BULLISH             = 1
DIR_SIDEWAYS            = 0
MACRO_TREND_DAYS        = 200
MICRO_TREND_INTERVALS   = 20
TREND_SLOPE_EPSILON     = 0.0001
MIN_GROWTH_RATIO        = 1.5
MAX_OPPOSITE_WICK       = 0.1
STRONG_HEIGHT_RATIO     = 1.3
RSI_LENGTH              = 14
RSI_OVERBOUGHT          = 80
RSI_OVERSOLD            = 20

getGradeFromScore(score) =>
    if(score >= 95)
        "A+"
    else if(score >= 90)
        "A"
    else if(score >= 80)
        "B"
    else if(score >= 70)
        "C"
    else
        "F"

getTrendDirection(p0, p1, epsilon) =>
    s = p0/p1

    if(s < 1 - epsilon)
        DIR_BEARISH
    else if(s > 1 + epsilon)
        DIR_BULLISH
    else
        DIR_SIDEWAYS

getTrendColor(trend, isBright) =>
    if isBright
        switch trend
            DIR_BEARISH => BEARISH_COLOR_BRIGHT
            DIR_BULLISH => BULLISH_COLOR_BRIGHT
            DIR_SIDEWAYS => SIDEWAYS_COLOR
    else
        switch trend
            DIR_BEARISH => BEARISH_COLOR_DIM
            DIR_BULLISH => BULLISH_COLOR_DIM
            DIR_SIDEWAYS => SIDEWAYS_COLOR

//
// Candle Analysis, Pivot Detection
//

// use Heiken-Ashi (smoothed) candles for most things
ticker = syminfo.tickerid
haTicker = ticker.heikinashi(ticker)
haClose = request.security(haTicker, timeframe.period, close)
haOpen = request.security(haTicker, timeframe.period, open)
haHigh = request.security(haTicker, timeframe.period, high)
haLow = request.security(haTicker, timeframe.period, low)

d0 = haClose[0] - haOpen[0]
d1 = haClose[1] - haOpen[1]
d2 = haClose[2] - haOpen[2]
d3 = haClose[3] - haOpen[3]
dir0 = (d0 > 0) ? DIR_BULLISH : DIR_BEARISH
dir1 = (d1 > 0) ? DIR_BULLISH : DIR_BEARISH
dir2 = (d2 > 0) ? DIR_BULLISH : DIR_BEARISH
dir3 = (d3 > 0) ? DIR_BULLISH : DIR_BEARISH
// minimum height of one avoids division by zero
h0 = math.max(math.abs(d0), 1)
h1 = math.max(math.abs(d1), 1)
h2 = math.max(math.abs(d2), 1)
oppositeWickLength = (dir0 == DIR_BEARISH)
     ? (haHigh[0] - math.max(haOpen[0], haClose[0])) // upper
     : (math.min(haOpen[0], haClose[0]) - haLow[0])  // lower

// 2 candles one direction followed by 2 the opposite direction indicate a pivot
directionsCheck     = dir2 != dir1 and dir3 == dir2 and dir1 == dir0
growthRatioCheck    = h0/math.min(h1, h2) >= MIN_GROWTH_RATIO
heightCheck         = h0 > h1
oppWickCheck        = oppositeWickLength <= MAX_OPPOSITE_WICK
pivotDir            = (directionsCheck and growthRatioCheck and heightCheck and oppWickCheck) ? dir0 : DIR_SIDEWAYS

// Candle Height Ratio
hSum = 0.0  
for i = 0 to MICRO_TREND_INTERVALS - 1
    hSum += math.abs(haClose[i] - haOpen[i])
avgCandleHeight = hSum/MICRO_TREND_INTERVALS
candleHeightRatio = h0/avgCandleHeight

// Macro Trend on the Daily
macroAvg = ta.ema(close, MACRO_TREND_DAYS)
macroAvg0 = request.security(ticker, "D", macroAvg[0])  // today
macroAvg1 = request.security(ticker, "D", macroAvg[1])  // yesterday
macroTrend = getTrendDirection(macroAvg0, macroAvg1, TREND_SLOPE_EPSILON)
// macro plot is not useful on a daily chart except to see trend color
// macroSeries = request.security(ticker, "D", macroAvg)
// plot(macroSeries, title="Macro", color=getTrendColor(macroTrend, false), linewidth=5)

// Micro Trend on the 5M
microAvg = ta.ema(haClose, MICRO_TREND_INTERVALS)
microTrend = getTrendDirection(microAvg[0], microAvg[1], TREND_SLOPE_EPSILON)
trendsMatch = (microTrend == macroTrend)
microTrendColor = getTrendColor(microTrend, trendsMatch)

// Put/Call Ratio
putCallRatio = request.security("USI:PCC", timeframe.period, close)
putCallDir = (putCallRatio > 1) ? DIR_BEARISH : DIR_BULLISH

// RSI
rsiValue = ta.rsi(close, RSI_LENGTH)

// Bollinger Bands Squeeze
keltPrd = 20
keltFactor = 1.5
cciPeriod = 50
bbDeviations = 2.0
bbPeriod = 20
bbD = ta.cci(close, cciPeriod)
bbDiff = ta.atr(keltPrd)*keltFactor
bbStd = ta.stdev(close, bbPeriod)
bbS = bbDeviations*bbStd/bbDiff
bbSqueeze = bbS < 1 and bbD > 0 or bbS < 1 and bbD <= 0
midY = (open + close)/2
plot(bbSqueeze ? midY : na, title="BB Squeeze", color=bbSqueeze ? SQUEEZE_COLOR : CLEAR_COLOR, style=plot.style_circles, linewidth=10)

// draw micro trend line last, after BB squeeze
plot(microAvg, title="Micro Trend", color=microTrendColor, linewidth=3)

//
// Score & Grade
// 
score = (pivotDir != DIR_SIDEWAYS) ? 65 : 0
scoreInfo = array.new<string>()

if(candleHeightRatio >= STRONG_HEIGHT_RATIO)
    score += 5
    scoreInfo.push("strong height")

if(pivotDir == macroTrend)
    score += 5
    scoreInfo.push("macro trend")

if(pivotDir == microTrend)
    score += 10
    scoreInfo.push("mictro trend")

    // only score BB squeeze if pivot direction matches micro trend
    if(bbSqueeze)
        score += 5
        scoreInfo.push("squeeze")

if(pivotDir == putCallDir)
    score += 5
    scoreInfo.push((putCallDir == DIR_BEARISH) ? "puts" : "calls")

if(pivotDir == DIR_BEARISH and rsiValue < 50 and rsiValue > RSI_OVERSOLD)
    score += 5
    scoreInfo.push("RSI-lo")
else if(pivotDir == DIR_BULLISH and rsiValue > 50 and rsiValue < RSI_OVERBOUGHT)
    score += 5
    scoreInfo.push("RSI-hi")

grade = getGradeFromScore(score)

if(grade != "F")
    hoverText = str.tostring(score)
         +((scoreInfo.size() != 0) ? (": "+scoreInfo.join(", ")) : "")
    labelColor = (pivotDir == DIR_BEARISH) ? BEARISH_COLOR : BULLISH_COLOR

    if(pivotDir == DIR_BEARISH)
        label.new(bar_index, high + BUY_LABEL_MARGIN, text="PUT "+grade, tooltip=hoverText, color=labelColor, style=label.style_label_down)
    else if(pivotDir == DIR_BULLISH)
        label.new(bar_index, low - BUY_LABEL_MARGIN, text="CALL "+grade, tooltip=hoverText, color=labelColor, style=label.style_label_up)

alertcondition(grade != "F", title="Buy Signal", message="{ticker}")

